import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.9.10'
    id 'com.gradle.plugin-publish' version '0.15.0'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

def pluginVersion = '1.6'

group = 'com.guardsquare'
version = pluginVersion

repositories {
    google()
    mavenCentral()
}

gradlePlugin {
    plugins {
        "appsweep" {
            id = 'com.guardsquare.appsweep'
            implementationClass = 'com.guardsquare.appsweep.gradle.AppSweepPlugin'
        }
    }
}

pluginBundle {
    website = 'https://guardsquare.com/appsweep-mobile-application-security-testing'
    vcsUrl = 'https://github.com/guardsquare/appsweep-gradle'
    description = 'Continuous Integration of app scanning using Guardsquare AppSweep.'
    tags = ['appsweep', 'android-development', 'security-tools', 'app-testing']

    plugins {
        "appsweep" {
            displayName = 'Guardsquare AppSweep Gradle Plugin'
        }
    }

    mavenCoordinates {
        groupId = 'com.guardsquare'
        artifactId = 'appsweep-gradle'
        version = pluginVersion
    }
}

def agpVersion = '8.1.2'

dependencies {
    shadow localGroovy()
    shadow gradleApi()

    implementation 'com.squareup.okhttp3:okhttp:4.3.1'
    implementation 'com.squareup.moshi:moshi:1.15.0'
    implementation 'com.squareup.moshi:moshi-kotlin:1.15.0'
    implementation 'com.squareup.moshi:moshi-kotlin-codegen:1.15.0'

    compileOnly("com.android.tools.build:gradle:$agpVersion")
    testCompileOnly("com.android.tools.build:gradle:$agpVersion")

    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:1.9.0"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:1.9.0"
    testImplementation 'io.kotest:kotest-runner-junit5-jvm:5.5.0'
    testImplementation 'io.kotest:kotest-assertions-core-jvm:5.5.0'
    testImplementation 'io.kotest:kotest-property-jvm:5.5.0'
}

test {
    useJUnitPlatform()
}

tasks.withType(Test).configureEach {
    systemProperty "agp.version", agpVersion
    systemProperty "appsweep.test", true
}


publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.guardsquare'
            artifactId = 'appsweep-gradle'
            version = pluginVersion

            from components.java
        }
    }
}


tasks.register('relocateShadowJar', ConfigureShadowRelocation) {
    target = tasks.shadowJar
}

tasks.shadowJar.dependsOn tasks.relocateShadowJar
